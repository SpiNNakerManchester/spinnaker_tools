
/*
// sark.lnk	    GNU linker script for SARK/SpiNNaker applications
//
// Copyright (C)    The University of Manchester - 2011-2013
//
// Author           Steve Temple, APT Group, School of Computer Science
//
// Email            temples@cs.man.ac.uk
//

// Copyright (c) 2011-2019 The University of Manchester
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

*/


MEMORY
{
    ITCM : ORIGIN = 0, LENGTH = 0x7f00
    DTCM : ORIGIN = 0x400000, LENGTH = 0xf800
}


SECTIONS
{
    ITCM_BASE = 0x00000000;
    DTCM_BASE = 0x00400000;

    STACK_TOP = 0x00410000;
    STACK_SIZE = 0x800;

    APLX_SIZE = 64;
    APLX_ENTRY = 16;

    . = ITCM_BASE;

    RO_DATA :
    {
          * (_alib_reset);
          * (alib_*);
          * (.text*);
          __init_array_start = .;
          KEEP (*(.init_array*))
          __init_array_end = .;
          * (.rodata*);
          * (.fini*);
          * (.init*);
    } > ITCM

    . = ALIGN (4);

    /*
      Attempt to deal with .ARM.exdix* sections. Ideally we probably
      want to throw these away but for now they go into the RO binary
      See also "objcopy" lines in make file...
    */

    EX_DATA :
    {
        * (.ARM.exidx*);
    } > ITCM

    RO_LENGTH = . - ITCM_BASE;

    . = DTCM_BASE;

    RW_DATA  :
    {
        * (.data*);
    } > DTCM

    . = ALIGN (4);

    RW_LENGTH = . - DTCM_BASE;

    ZI_DATA  :
    {
	* (.bss*);
	* (COMMON);
    } > DTCM

    . = ALIGN (4);

    HEAP_BASE = .;

    RO_BASE = ITCM_BASE;
    RO_FROM = APLX_SIZE;

    RW_BASE = DTCM_BASE;
    RW_FROM = APLX_SIZE + RO_LENGTH - APLX_ENTRY;

    ZI_BASE = RW_BASE + RW_LENGTH;
    ZI_LENGTH = HEAP_BASE - ZI_BASE;
}
